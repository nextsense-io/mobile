apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.objectbox'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11.
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            proto {
                srcDir "$projectDir/src/main/proto"
            }
            java {
                srcDir "${protobuf.generatedFilesBaseDir}/source/proto"
            }
        }
    }

    buildToolsVersion '30.0.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}



sonarqube {
    properties {
        property "sonar.projectName", "android_base_library"
        property "sonar.projectKey", "nextsense-io_data_analysis"
        property "sonar.tests", ["src/test/java"]
        property "sonar.test.inclusions", "**/*Test*/**"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java"
        property "sonar.exclusions", '**/*Test*/**,' +
                '*.json,' +
                '**/*test*/**,' +
                '**/.gradle/**,' +
                '**/R.class'
    }
}

dependencies {
    // Getting an error when upgrading to 2.x.
    // Failed to transform FakeDependency.jar to match attributes {artifactType=_internal-d8-desugar-methods, org.gradle.libraryelements=jar, org.gradle.usage=java-runtime}. Execution failed for D8BackportedMethodsGenerator
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.test:core:1.4.0'
    implementation 'com.github.weliem:blessed-android:2.3.0'
    // implementation 'com.github.nextsense-io:blessed-android:2.0.11-nextsense'
    implementation platform('com.google.firebase:firebase-bom:30.5.0')
    compileOnly "com.google.firebase:firebase-analytics"
    compileOnly "com.google.firebase:firebase-firestore"
    implementation "com.google.firebase:firebase-functions"
    implementation 'com.google.code.gson:gson:2.9.1'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.google.protobuf:protobuf-javalite:3.21.1'
    implementation 'com.google.protobuf:protoc:3.13.0'
    implementation 'org.greenrobot:eventbus:3.3.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.9'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.nextsense-io.android_base_library.core'
                artifactId = 'core'
                version = '0.1.0'

            }
            debug(MavenPublication) {
                from components.debug
                groupId = 'com.github.nextsense-io.android_base_library.core'
                artifactId = 'core-debug'
                version = '0.1.0'
            }
        }
    }
}
