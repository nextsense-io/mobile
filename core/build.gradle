apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.objectbox'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

group 'com.github.nextsense-io.android_base_library'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11.
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "android_base_library"
        property "sonar.projectKey", "nextsense-io_data_analysis"
        property "sonar.tests", ["src/test/java"]
        property "sonar.test.inclusions", "**/*Test*/**"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java"
        property "sonar.exclusions", '**/*Test*/**,' +
                '*.json,' +
                '**/*test*/**,' +
                '**/.gradle/**,' +
                '**/R.class'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.test:core:1.4.0'
    implementation 'com.github.weliem:blessed-android:2.0.8'
    implementation platform('com.google.firebase:firebase-bom:28.3.0')
    compileOnly "com.google.firebase:firebase-analytics"
    compileOnly "com.google.firebase:firebase-firestore"
    compileOnly "com.google.firebase:firebase-functions"
    implementation 'com.google.guava:guava:30.0-android'
    implementation 'com.google.protobuf:protobuf-javalite:3.14.0'
    implementation 'com.google.protobuf:protoc:3.13.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    // implementation 'com.jakewharton.timber:timber:4.7.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.nextsense-io.android_base_library.core'
                artifactId = 'core'

            }
            debug(MavenPublication) {
                from components.debug
                groupId = 'com.github.nextsense-io.android_base_library.core'
                artifactId = 'core-debug'
            }
        }
    }
}
