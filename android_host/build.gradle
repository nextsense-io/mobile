plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

android {
    signingConfigs {
        // Need to add these values to your local gradle.properties file.
        release {
            storeFile file(RELEASE_STORE_FILE)
            keyAlias RELEASE_KEY_ALIAS
            storePassword RELEASE_STORE_PASSWORD
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    namespace "io.nextsense.android.main"

    defaultConfig {
        applicationId "io.nextsense.android.main"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 28
        versionName "0.3.26"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // Filter for architectures supported by Flutter.
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }

    flavorDimensions "target_user_type", "env"
    productFlavors {
        subject {
            getIsDefault().set(true)
            dimension "target_user_type"
            applicationIdSuffix ".study"
        }

        researcher {
//            getIsDefault().set(true)
            dimension "target_user_type"
            applicationIdSuffix ".research"
        }

        dev {
//            getIsDefault().set(true)
            dimension "env"
            applicationIdSuffix ".dev"
        }

        prod {
            getIsDefault().set(true)
            dimension "env"
            applicationIdSuffix ".prod"
        }
    }

    sourceSets {
        researcherDev {
            setRoot 'src/researcher/dev'
        }

        researcherProd {
            setRoot 'src/researcher/prod'
        }

        subjectDev {
            setRoot 'src/subject/dev'
        }

        subjectProd {
            setRoot 'src/subject/prod'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11.
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'
    implementation project(path: ':core')
    implementation project(path: ':flutter')
    implementation project(path: ':service')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-auth:20.4.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.code.gson:gson:2.9.1'
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-appcheck-debug'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

apply plugin: 'com.google.gms.google-services'
// This must appear at the bottom of the file
apply plugin: 'com.google.firebase.crashlytics'