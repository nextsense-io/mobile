plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

android {
    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            // Need to add these values to your local gradle.properties file.
            storeFile file(RELEASE_STORE_FILE)
            keyAlias RELEASE_KEY_ALIAS
            storePassword RELEASE_STORE_PASSWORD
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    compileSdk 34
    namespace "io.nextsense.android.main"

    defaultConfig {
        applicationId "io.nextsense.android.main"
        minSdkVersion 28
        targetSdkVersion 34
        versionCode 56
        versionName "0.7.12"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // Filter for architectures supported by Flutter.
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    flavorDimensions "target_user_type", "env"
    productFlavors {
        subject {
            getIsDefault().set(true)
            dimension "target_user_type"
            applicationIdSuffix ".study"
        }

        researcher {
            dimension "target_user_type"
            applicationIdSuffix ".research"
        }

        consumer {
            dimension "target_user_type"
            applicationIdSuffix ".consumer"
        }

        lucid {
            dimension "target_user_type"
            applicationIdSuffix ".lucid"
        }

        scratch {
            dimension "env"
            applicationIdSuffix ".scratch"
        }

        dev {
            dimension "env"
            applicationIdSuffix ".dev"
        }

        prod {
            getIsDefault().set(true)
            dimension "env"
            applicationIdSuffix ".prod"
        }
    }

    sourceSets {
        researcherDev {
            setRoot 'src/researcher/dev'
        }

        researcherProd {
            setRoot 'src/researcher/prod'
        }

        subjectDev {
            setRoot 'src/subject/dev'
        }

        subjectProd {
            setRoot 'src/subject/prod'
        }

        consumerDev {
            setRoot 'src/consumer/dev'
        }

        consumerProd {
            setRoot 'src/consumer/prod'
        }

        lucidDev {
            setRoot 'src/lucid/dev'
        }

        lucidProd {
            setRoot 'src/lucid/prod'
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs.
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 11.
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig true
    }

}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    implementation project(path: ':core')
    implementation project(path: ':flutter')
    implementation project(path: ':service')
    lucidWearApp(project(":lucidwatchapp"))
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-auth:21.1.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-appcheck-debug'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    consumerImplementation 'org.tensorflow:tensorflow-lite:2.14.0'
    consumerImplementation 'org.tensorflow:tensorflow-lite-select-tf-ops:2.14.0'
}

apply plugin: 'com.google.gms.google-services'
// This must appear at the bottom of the file
apply plugin: 'com.google.firebase.crashlytics'